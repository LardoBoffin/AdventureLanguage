'in notepad++ set the language to Visual Basic
'any lines starting with ' will be ignored
'any line starting with - will only be included if adventureData has <BuildType>Debug</BuildType> set

MODE @ScreenMode@:VDU23;8202;0;0;0;:PRINT"Loading";:T%=0:Q%=0:DIM IX% @RMIndex@:DIM RM% @Room@

-SW%=0   
-SC%=0
-SO%=0
   
'start of main loop
PROCldSM:REPEAT:PROCmsg(0):PROCldOb:PROCldRm:IF T%=0 THEN PROCgW(1):PROCgW(2):PROCgW(4)
		   
	'@ScreenMode@ is set in Adventure.xml   
	CLS:PRINT:PRINTCHR$(131);CHR$(141);TAB(5)"@Title@":PRINTCHR$(131);CHR$(141);TAB(5)"@Title@"  
	   
	'@NumVar@ is set in Adventure.xml
	M$=STRING$(255," "):PROCvars:DIM CTR% @NumVar@:FOR X%=1 TO @NumVar@:PROCset(X%,0):NEXT:R%=1:OLR%=0:T%=0:PL%=255:EX%=0:ENC%=0:CW%=0:L%=0:PROCinit
	PRINT TAB(0,5)"Press";CHR$(131);"1";CHR$(135);"to start a new game":PRINT TAB(0,7)"Press"CHR$(131);"2";CHR$(135);"to continue an existing game":PRINT:PRINT">";:REPEAT:C$=INKEY$(0):UNTIL C$="1" OR C$="2":CLS
	IF C$="2" THEN PRINTTAB(10,0)CHR$(131);"Getting catalogue...":PRINT:*CAT
	IF C$="2" THEN PRINT:PRINT"Type the file name and press Enter":PRINT"Or press Enter for the default save":INPUT">" M$:IF LEFT$(M$,2)="S." THEN M$=RIGHT$(M$,LEN(M$)-2)
	IF C$="2" THEN M$="LOAD "+M$:CLS:PROCload
	
	REPEAT:D%=RND(100):PROCpreRoom:IF R%<>OLR% THEN OLR%=R%:PROCcl:PROCgtRm:PROCt
	D%=FNhighPty:IF EX%=0 AND NOT D% THEN PROCpsTxt
-TM%=TIME
		IF V%=0 AND EX%=0 AND NOT D% THEN PROCmsg(@_What):D%=TRUE
		IF EX%=0 AND NOT D% THEN D%=FNckMv:IF NOT D% THEN D%=FNlowPty:IF V%>0 AND NOT D% THEN PROCmsg(@_CannotDoIt)
-PRINT "Logic time "; (TIME-TM%)/100
-IF SC%=1 PROCshowC
CL%=0:D%=0:UNTIL EX%=1:UNTIL FNagain=FALSE:PRINT "Thanks for playing...":END

'debug stuff
-DEFPROCshowC:LOCAL X%
-PRINT:FOR X%=1 TO @NumVar@:PRINT X%,FNget(X%):NEXT:
-ENDPROC

-DEFPROCshowParse
-PRINT:PRINT "Verb",V%
-PRINT "Adverb",AVRB%
-PRINT "Noun",N%
-PRINT "Last Noun",L%
-PRINT "Noun2",NO2%:PRINT
-ENDPROC

'if CLS is set then clear the screen
DEFPROCcl:IF Q%=1 THEN CLS
ENDPROC
  
'loading and saving functions
DEFFNagain:PRINT:PROCmsg(@_AnotherGame):INPUT M$:IF M$="Y" OR M$="y" THEN =TRUE ELSE =FALSE

'save the game
DEFPROCsave:LOCAL F%,X%,N$:N$=LEFT$(MID$(M$,6),7):IF N$="" THEN N$="SAVE-M"
	PRINT"Saving...":F%=OPENOUT(N$):PRINT#F%,T%,R%,ENC%,CW%,Q%:FOR X%=1 TO @NumVar@:BPUT#F%,?(CTR%+X%):NEXT:IF OBJN%>0 THEN FOR X%=1 TO OBJN%*8:BPUT#F%,?(OB%+X%):NEXT:FOR X%=1 TO @Room@:BPUT#F%,?(RM%+X%):NEXT
CLOSE#F%:PROCend:PRINT"Done.":ENDPROC

'load the game
DEFPROCload:LOCAL F%,X%,N$:N$=LEFT$(MID$(M$,6),7):IF N$="" THEN N$="SAVE-M"
	PROCmsg(@_Loading):F%=OPENIN(N$):INPUT#F%,T%,R%,ENC%,CW%,Q%:FOR X%=1 TO @NumVar@:?(CTR%+X%)=BGET#F%:NEXT:IF OBJN%>0 THEN FOR X%=1 TO OBJN%*8:?(OB%+X%)=BGET#F%:NEXT:FOR X%=1 TO @Room@:?(RM%+X%)=BGET#F%:NEXT
CLOSE#F%:PROCend:PRINT"Done.":ENDPROC

'load rooms index and room
DEFPROCldRm:OSCLI("L. IDX-R "+STR$~IX%):OSCLI("L. DATA-R "+STR$~RM%):ENDPROC

'load messages
'@NumMssages@ is automatically calculated
DEFPROCldSM:LOCAL R%,F%:F%=OPENIN("DATA-M"):DIM MS$(@NumMssages@):FOR X%=1 TO @NumMssages@:I%=(X%-1)*5:IND=OPENIN("IDX-M"):PTR#IND=I%:INPUT#IND,I%:CLOSE#IND:PTR#F%=I%:INPUT#F%,MS$(X%):PRINT".";:NEXT:CLOSE#F%:ENDPROC

'load objects
DEFPROCldOb:LOCAL R%:F%=OPENIN("DATA-O"):IF FNturn=0 THEN OBJN%=@NumObjects@:DIM OB% @ObjectData@
R%=1:REPEAT:?(OB%+R%)=BGET#F%:R%=R%+1:UNTIL EOF#F%:CLOSE#F%:ENDPROC
  
'store object at location
'DEFPROCstOb(O%,R%):LOCAL P%:P%=((O%-1)*8)+4:?(OB%+P%)=R% DIV 256:?(OB%+P%+1)=R% MOD 256:ENDPROC
'DEFPROCstOb(O%,R%):LOCAL P%:P%=((O%-1)*7)+4:?(OB%+P%)=R%:ENDPROC
DEFPROCstOb(O%,R%):?(OB%+((O%-1)*@SizeOfObject@)+4)=R%:ENDPROC



'list objects at location
DEFPROClsOb(R%,MS%):LOCAL X%,O%:O%=0:IF OBJN%=0 THEN ENDPROC
	FOR X%=1TOOBJN%:M%=FNobHr(X%,R%):IF M%>0 THEN O%=O%+1:NEXT ELSE NEXT
	IF O%=0 AND MS%>1 THEN PRINT:ENDPROC
	IF O%=0 AND MS%=1 THEN PROCmsg(@_Carrying):PROCmsg(@_Nothing):ENDPROC
	PRINT:IF MS%>0 THEN PROCmsg(MS%):PRINT
	FOR X%=1TOOBJN%:M%=FNobHr(X%,R%):IF M%>0 THEN PRINTCHR$(131);:PROCmsg(M%):PRINT:NEXT ELSE NEXT
ENDPROC

'list locations of all objects
-DEFPROClsAllOb:LOCAL X%,L%,M%
-FOR X%=1TOOBJN%:L%=FNobLn(X%)
-M%=FNobMn(X%)
-PRINT L%;:PROCmsg(M%):PRINT
-NEXT 
-ENDPROC

'is object here?
DEFFNobHr(O%,R%):IF FNobLn(O%)=R% THEN =FNobMn(O%) ELSE =0

'return object location
DEFFNobLn(O%):=?(((O%-1)*@SizeOfObject@)+4+OB%)
  
'return object message number
DEFFNobMn(O%):U%=((O%-1)*@SizeOfObject@)+2+OB%:=?(U%)*256+?(U%+1)
  
'set object message number
DEFPROCsetObjMsg(O%,M%):LOCAL L%:L%=OB%+((O%-1)*@SizeOfObject@)+2:?(L%)=M% DIV 256:?(L%+1)=M% MOD 256:ENDPROC

'return object weight
DEFFNobWt(O%):=?(((O%-1)*@SizeOfObject@)+6+OB%)
  
'check to see if the move is valid
DEFFNckMv:LOCAL N%,Z%:D%=FALSE:N%=?(RM%+K%):IF N%=0 THEN =FALSE 
	'get move data directly from the room data section of RM%. The position is stored in K%
	 FOR C%=1 TO (N%*2) STEP 2:Z%=RM%+K%+C%:IF ?(Z%)=V% THEN R%=?(Z%+1):D%=TRUE:NEXT ELSE NEXT
=D%

'get location of desired room in the room index
DEFFNrmPtr(R%):N%=(R%-1)*2:=(?(IX%+N%)*256)+?(IX%+N%+1)
  
'get room details
DEFPROCgtRm:LOCAL F%,N%,M%,D%,C%,V%:D%=0:F%=FNrmPtr(R%):G%=?(RM%+F%):IF FNflag(0)=TRUE AND FNon(1)=FALSE THEN D%=1:PROCmsg(@_ItsDark)

	'get the position in the index data and get the value of the pointer
	

	'print all room messages if not dark
	F%=F%+1:N%=?(RM%+F%):F%=F%+1:FOR C%=1TON%:M%=?(RM%+F%)*256:F%=F%+1:M%=M%+?(RM%+F%):IF D%=0 THEN PROCmsg(M%):NEXT ELSE NEXT

	'load the directions, K% is the start point of the exits data
	F%=F%+1:N%=?(RM%+F%):K%=F%:IF D%=0 THEN PROClsOb(R%,15)
	IF N%=0 PROCrmMsg:ENDPROC

	PROCrmMsg:F%=F%+1:PRINT:PRINT "Exits are : ";:FOR M%=0 TO (N%-1)*2 STEP 2:V%=?(RM%+F%+M%):IF V%>127 THEN V%=V%-128
	'print out the directions
	
	C%=0:REPEAT:IF VN%?(C%)=V% THEN PRINT V$(C%)+" ";:D%=2
	C%=C%+1:UNTIL D%=2 OR C%=VM%:D%=0:NEXT:PRINT:ENDPROC


'enable or disable exit
DEFPROCeE(N%):LOCAL U%,V%:U%=RM%+K%+1+((N%-1)*2):V%=?(U%):IF V%>127 THEN ?(U%)=V%-128 ELSE ?(U%)=V%+128
'verb number
'MSB of location link
'LSB of location link
'K% holds the start of the exit data, number of directions
'U%=position of verb for exit
PROCmsg(@_OK):ENDPROC

'load words (nouns, verbs and adverbs) from disc
DEFPROCgW(T%):LOCAL N%,R%,D$,RN%:R%=1:IF T%=1 THEN N%=OPENIN("DATA-N") ELSE IF T%=2 THEN N%=OPENIN("DATA-V") ELSE IF T%=4 THEN N%=OPENIN("DATA-A")
	REPEAT:RN%=BGET#N%:INPUT#N%,D$:R%=R%+1:UNTIL EOF#N%:PTR#N%=0:IF T%=1 THEN NM%=R%:DIM N$(R%):DIM NN% R% ELSE IF T%=2 THEN VM%=R%:DIM V$(R%):DIM VN% R% ELSE IF T%=4 THEN AM%=R%:DIM A$(R%):DIM AN% R%
	R%=1:REPEAT:RN%=BGET#N%:INPUT#N%,D$:IF T%=1 THEN N$(R%)=FNtU(D$):NN%?R%=RN% ELSE IF T%=2 THEN V$(R%)=FNtU(D$):VN%?R%=RN% ELSE IF T%=4 THEN A$(R%)=FNtU(D$):AN%?R%=RN%
R%=R%+1:PRINT".";:UNTIL EOF#N%:CLOSE#N%:ENDPROC
  
'print a message to the screen
DEFPROCmsg(M%):LOCAL S%,P%,H%:M$=MS$(M%)
	IF M%=0 THEN PRINT
	S%=1:P%=1:H%=POS:REPEAT:P%=INSTR(M$," ",S%):IF P%=0 THEN H%=H%+(LEN(M$)-S%)+1 ELSE H%=H%+(P%-S%)+1
	'@ScreenWidth@ is set in Adventure.xml
	IF H%>@ScreenWidth@ THEN PRINT
	PRINT MID$(M$,S%,P%-S%);" ";:H%=POS:S%=P%+1:UNTIL P%=0:IF POS=1 THEN VDU31,POS-1,VPOS:A%=135:C=USR(&FFF4):C=C AND &FFFF:C=C DIV &100:IF C>32 THEN VDU31,POS+1,VPOS
ENDPROC
   
'parse the text
DEFPROCpsTxt:LOCAL WL%,C%,S%,U%:PROCvars:REPEAT:PRINT:PROCmsg(@_WhatNow):U%=VPOS:INPUT TAB(LEN(MS$(2)),U%),M$:UNTIL M$>"":M$=FNtU(M$):S%=1:IF M$="*." THEN D%=TRUE:*.

'show counters etc.
-LOCAL T%:T%=TIME
-IF M$="*C" THEN PROCshowC:D%=TRUE
-IF LEFT$(M$,4)="*WON" THEN SW%=1:D%=TRUE
-IF LEFT$(M$,5)="*WOFF" THEN SW%=0:D%=TRUE
-IF LEFT$(M$,4)="*CON" THEN SC%=1:D%=TRUE
-IF LEFT$(M$,5)="*COFF" THEN SC%=0:D%=TRUE
-IF LEFT$(M$,4)="*OON" THEN SO%=1:D%=TRUE

	U%=LEN(M$)
	REPEAT:WRD$=FNgW(MID$(M$,S%,U%-S%+1)):WL%=LEN(WRD$):S%=S%+WL%+1:IF WRD$="IT" THEN N%=L%:C%=1
		IF V%=0 THEN V%=FNpV(WRD$,WL%):IF V%>0 THEN C%=1
		IF N%=0 THEN N%=FNpN(WRD$,WL%):IF N%>0 THEN C%=1:L%=N%
		IF AVRB%=0 THEN AVRB%=FNpA(WRD$,WL%):IF AVRB%>0 THEN C%=1
		IF C%=0 THEN IF NO2%=0 THEN NO2%=FNpN(WRD$,WL%):IF NO2%>0THEN C%=1:IF NO2%=N% THEN NO2%=0
		IF C%=0 THEN S%=U%
C%=0:UNTIL S%>=U%
-IF SO%=1 PROClsAllOb
-IF SW%=1 PROCshowParse
-PRINT "Parse time "; (TIME-T%)/100
ENDPROC
  
'parse a verb
DEFFNpV(T$,E%):LOCAL W%,F%,R%:W%=0:R%=1:REPEAT:F%=LEN(V$(R%)):IF E%=F% THEN IF T$=V$(R%) THEN W%=R%
		R%=R%+1:UNTIL R%=VM% OR W%>0:IF W%>0 THEN =VN%?W% ELSE=0

DEFFNpN(T$,E%):LOCAL W%,F%,R%:W%=0:R%=1:REPEAT:F%=LEN(N$(R%)):IF E%=F% THEN IF T$=N$(R%) THEN W%=R%
		R%=R%+1:UNTIL R%=NM% OR W%>0:IF W%>0 THEN =NN%?W% ELSE=0

DEFFNpA(T$,E%):LOCAL W%,F%,R%:W%=0:R%=1:REPEAT:F%=LEN(A$(R%)):IF E%=F% THEN IF T$=A$(R%) THEN W%=R%
		R%=R%+1:UNTIL R%=AM% OR W%>0:IF W%>0 THEN =AN%?W% ELSE=0

'to upper case
DEFFNtU(T$):LOCAL P%,L%:M$="":L%=LEN(T$):FOR P%=1TOL%:C%=ASC(MID$(T$,P%,1)):M$=M$+CHR$(C%+((C%>90)*32)):NEXT:=M$

'get a word (ending in a space)
DEFFNgW(T$):LOCAL C%,C$,WRD$:C%=1:REPEAT:C$=MID$(T$,C%,1):WRD$=WRD$+C$:C%=C%+1:UNTIL C$=" "OR C%>LEN(T$):IF RIGHT$(WRD$,1)<>" "THEN WRD$=WRD$+" " 
=LEFT$(WRD$,LEN(WRD$)-1)
  
'set the variables
DEFPROCvars:M$="":N%=0:NO2%=0:V%=0:AVRB%=0:OBJ%=0:WRD$="":N%=0:D%=0:ENDPROC
  
'take an object
DEFPROCtake(O%):IF FNobHr(O%,R%)=0 THEN PROCmsg(@_CannotFindIt):ENDPROC
	IF CW%+FNobWt(O%)>ENC% THEN PROCmsg(@_TooHeavy):ENDPROC
PROCstOb(O%,PL%):PROCmsg(@_OK):PROCt:CW%=CW%+FNobWt(O%):ENDPROC

'swap object 1 with 2. 1 goes to location 0, 2 goes to player inv  
DEFPROCswap(O1%,O2%):PROCstOb(O1%,0):PROCstOb(O2%,PL%):ENDPROC

'move the object O% to R% (room)
DEFPROCmove(O%,R%):PROCstOb(O%,R%):ENDPROC

'drop the object (if present) 
DEFPROCdrop(O%):IF FNobHr(O%,PL%)=0:PROCmsg(@_HaveNotGotThat):ENDPROC
	PROCstOb(O%,R%):PROCmsg(@_OK):PROCt:CW%=CW%-FNobWt(O%):ENDPROC
	
'list player objects
DEFPROCinv:PROClsOb(PL%,1):ENDPROC

'return the message number for the object
DEFPROCobj(O%):PROCmsg(FNobMn(O%)):ENDPROC

'list objects in the R% (room)
DEFPROClist(R%):PROClsOb(R%,0):ENDPROC

'go to the room RM%
DEFPROCgoto(RO%):R%=RO%:ENDPROC

'move to the room where object O% is
DEFPROCfind(O%):PROCgoto(FNobLn(O%)):ENDPROC

'increase the turn counter
DEFPROCt:T%=T%+1:ENDPROC

'set the encumbrance value to D%
DEFPROCenc(D%):ENC%=D%:ENDPROC

'return true or false for flag 0 to 7
DEFFNflag(FL%):IF G% AND 2^FL% THEN =TRUE ELSE =FALSE

'return true if counter F% is on (>0)
DEFFNon(F%):IF ?(CTR%+F%)>0 THEN =TRUE ELSE =FALSE

'increase counter (C%) by 1 unless 255
DEFPROCinc(C%):IF FNget(C%)<256 THEN PROCset(C%,FNget(C%)+1)
ENDPROC

'decrease counter (C%) by 1 unless 0
'DEFPROCdec(C%):LOCAL T%:T%=FNget(C%):IF T%>0 THEN T%=T%-1:PROCset(C%,T%)
DEFPROCdec(C%):IF FNget(C%)>0 THEN PROCset(C%,FNget(C%)-1)
ENDPROC

'hold for the specified time
DEFPROChold(T%):LOCAL FT%:FT%=TIME+T%:REPEAT:UNTIL TIME>=FT%:ENDPROC

'set counter (C%) to the value T%
DEFPROCset(C%,T%):?(CTR%+C%)=T%:ENDPROC

'set counter (C%) to 1
DEFPROCon(C%):?(CTR%+C%)=1:ENDPROC

'set counter (C%) to 0
DEFPROCoff(C%):?(CTR%+C%)=0:ENDPROC

'return value of counter *C%)
DEFFNget(C%):=?(CTR%+C%)

'set value of Q% (cls setting) and say OK.
DEFPROCcls(S%):Q%=S%:PROCend:PROCmsg(@_OK):ENDPROC

'return if object (O%) is in the current room
DEFFNhere(O%):=FNobHr(O%,R%)>0 

'return if object (O%) is in inventory (PL%)
DEFFNinInv(O%):=FNobHr(O%,PL%)>0

'return if object is either in the room or in inventory
DEFFNavail(O%)=FNhere(O%) OR FNinInv(O%)

'get a random number
DEFFNrn(N%)=RND(N%)

'end the turn
DEFPROCend:D%=1:ENDPROC

'exit the current game (e.g. if dead)
DEFPROCexit:EX%=1:ENDPROC

'return the current turn number
DEFFNturn:=T%

'wait for a key press
DEFPROCwait:PRINT:PRINT"Please press a key.";:REPEAT UNTIL INKEY(0)<>-1:ENDPROC
 
'game logic goes into these sections

'runs once at start of game
DEFPROCinit
ENDPROC

'runs before the room is set 
DEFPROCpreRoom
ENDPROC

'called before high priority - designed to display custom messages for a specific room
DEFPROCrmMsg
ENDPROC

'high priority events before the player types anything
DEFFNhighPty
=FALSE

'parsed after the player types something
DEFFNlowPty
=FALSE
